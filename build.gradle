import groovy.sql.Sql

plugins {
    id 'org.springframework.boot' version '2.1.4.RELEASE'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id 'java'
}

group = 'com.shchur.dmitriy'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
    mavenCentral()
}

sourceSets.main.resources {
    srcDir "src/main/resources"
    srcDir "config"
}

configurations {
    hsqldb
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }

    hsqldb group: 'org.hsqldb', name: 'hsqldb', version: '2.4.0'

    compile 'org.hsqldb:hsqldb:2.4.0'
}

test {
    useJUnitPlatform()
}

URLClassLoader loader = GroovyObject.class.classLoader as URLClassLoader

configurations.hsqldb.each {
    File file ->
        loader.addURL(file.toURI().toURL())
        println "Added url: $file"
}

def executeScript (File scriptFile) {
    def props = new Properties()
    file("config/application.properties").withInputStream {props.load(it)}

    def con = getConnection(props)

    String[] scripts = scriptFile.text.split(";")
    scripts.each {String script -> if (!script.trim().isEmpty()) con.execute(script + ";")}
    con.close()
}

def importFromFile (File csvFile) {
    def props = new Properties()
    file("config/application.properties").withInputStream {props.load(it)}

    def con = getConnection(props)

    def people = con.dataSet("person")
    def photosHostAddress = props.getProperty("photos.host.address")

    csvFile.splitEachLine(", $photosHostAddress") {fields ->
        if (fields[1] != null) {
            people.add(name: fields[0], photo_path: fields[1])
        }
    }
}

static def getConnection (Properties props) {
    def url = props.getProperty("datasource.url")
    def user = props.getProperty("datasource.username")
    def pass = props.getProperty("datasource.password")
    def driver = props.getProperty("datasource.connector")

    return Sql.newInstance(url, user, pass, driver)
}

task startHsqldbServer (type: JavaExec) {
    classpath configurations.hsqldb
    main = 'org.hsqldb.server.Server'
}

task createTables {
    doLast {
        executeScript(file('sqlscripts/create.sql'))
    }
}

task cleanTables {
    doLast {
        executeScript(file('sqlscripts/clean.sql'))
    }
}

task dropTables {
    doLast {
        executeScript(file('sqlscripts/drop.sql'))
    }
}

task fillFromPeopleFile {
    doLast {
        importFromFile(file('people.csv'))
    }
}
